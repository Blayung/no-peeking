plugins {
    id "fabric-loom" version "1.7-SNAPSHOT"
    id "com.modrinth.minotaur" version "2.+"
}

repositories {
    maven { url "https://maven.shedaniel.me" }
    maven { url "https://maven.terraformersmc.com/releases" }
}

dependencies {
    minecraft "com.mojang:minecraft:1.21.1"
    mappings "net.fabricmc:yarn:1.21.1+build.3:v2"
    modImplementation "net.fabricmc:fabric-loader:0.16.5"

    modApi "com.terraformersmc:modmenu:11.0.2"
    modLocalRuntime "net.fabricmc.fabric-api:fabric-api:0.106.0+1.21.1"

    modApi("me.shedaniel.cloth:cloth-config-fabric:15.0.140") {
        exclude(group: "net.fabricmc.fabric-api")
    }
}

def modVersion = "1.1.2"

base.archivesName = "no-peeking-" + modVersion

def javaVersion = JavaVersion.VERSION_17

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

jar.from("LICENSE.txt")

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": modVersion
    }
}

modrinth {
    token = System.getenv("MINOTAUR_PAT")
    syncBodyFrom = rootProject.file("modrinth/readme.md").text
    uploadFile = remapJar
    projectId = "no_peeking"
    changelog = rootProject.file("modrinth/changelog.md").text
    versionName = "No Peeking " + modVersion
    versionNumber = modVersion
    gameVersions = ["1.20", "1.20.1", "1.20.2", "1.20.3", "1.20.4", "1.20.5", "1.20.6", "1.21", "1.21.1"]
    loaders = ["fabric", "quilt"]
    dependencies {
        required.project "cloth-config"
        optional.project "modmenu"
    }
}

task modrinthUpload {
    dependsOn "modrinth"
    dependsOn "modrinthSyncBody"
}
